plugins {
    id 'idea'
    id 'maven'
    id 'maven-publish'
}

group 'ru.shestakova'
version '1.0.0-SNAPSHOT'

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    ext {
        versions = [
                'spring_data_jpa': '2.1.10.RELEASE',
                'spring_boot'    : '2.1.7.RELEASE',
                'hibernate'      : '5.4.4.Final',
                'postgresql'     : '42.2.6',
                'persistence_api': '1.0.2',
                'guava'          : '28.0-jre',
                'lombok'         : '1.18.8',
                'junit'          : '5.5.1',
        ]
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    sourceCompatibility = 1.12
    targetCompatibility = 1.12

    project.buildDir = file("${rootProject.buildDir}/${project.name}")
    idea.module {
        iml {
            generateTo = file("${rootProject.projectDir}/.idea")
        }

        sourceDirs += file("$projectDir/src/main/java")
        resourceDirs += file("$projectDir/src/main/resources")
        testSourceDirs += file("$projectDir/src/test/java")
        testResourceDirs += file("$projectDir/src/test/resources")
        generatedSourceDirs += file("$buildDir/generated/")

        downloadJavadoc = true
        downloadSources = true
    }

    dependencies {
        compile group: 'org.postgresql', name: 'postgresql', version: versions.postgresql
        compile group: 'com.google.guava', name: 'guava', version: versions.guava

        compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit
    }

    if (project.plugins.hasPlugin("jacoco")) {
        jacoco {
            toolVersion = "0.8.4"
            reportsDir = file("$buildDir/customJacocoReportDir")
        }
    }
}

idea.project {

}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://localhost/tmp/myRepo/")
        }
    }
}